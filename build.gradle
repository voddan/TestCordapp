buildscript {
    ext.corda_release_group = 'net.corda'
    ext.corda_release_version = '3.3-corda'
    ext.corda_gradle_plugins_version = '3.0.9'
    ext.indy_version = "1.6.4"
    ext.cordentity_version = '0.5.0'
    ext.indy_utils_version = '0.3.0'
    ext.quasar_version = '0.7.9'
    ext.junit_version = '4.12'
    ext.kotlin_version = '1.3.10'
    ext.kotlinx_coroutines_version = '1.0.1'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordformation:${corda_gradle_plugins_version}"
    }
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://repo.evernym.com/artifactory/libindy-maven-local' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
        maven { url 'https://kotlin.bintray.com/kotlinx' }
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'kotlin'
    apply plugin: 'idea'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true   // Useful for reflection.
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    }
}

repositories {
    jcenter()
    maven { url 'https://repo.evernym.com/artifactory/libindy-maven-local' }
    maven { url 'https://jitpack.io' }
}


apply from: "corda-run.gradle"

dependencies {
    // CorDapp dependencies
    // Specify your cordapp's dependencies below, including dependent cordapps
    cordapp project(":cordapp")
    cordapp "$corda_release_group:corda-finance:$corda_release_version"
    cordapp "${corda_release_group}:corda-core:${corda_release_version}"

    cordapp "com.github.Luxoft.cordentity:cordapp:$cordentity_version"
    cordapp "com.github.Luxoft.cordentity:cordapp-contracts-states:$cordentity_version"
}

assemble {
    dependsOn deployCordaNetwork
}